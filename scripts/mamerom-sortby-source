#!/usr/bin/env zsh
#
# Hardlinks MAME roms to the .sorted directory based on the 'source' key
# within `mame -listxml`. This enables directory based cfg overrides,
# core options and controller remapping. A group of MAME "machines" or
# games for a given source are more likely to share specific settings.
#
# Usage: mamerom-sortby-source [-r|--reset]
#

zparseopts -D -E -F - {r,-reset}=reset

local base=~/Games/Emulation/Arcade
local sortdir=$base/.sorted
local infodir=$base/.rominfo/info
local thisdir=$(dirname `realpath $0`)

if ! $thisdir/mamerom-generate-infojs $base
then
  return 1
fi

if [[ ! -d $sortdir ]]
then
  if mkdir $sortdir
  then
    # exclude from time machine backups.
    tmutil addexclusion $sortdir
    tag -a "Time Machine - Excluded" $sortdir
  else
    return 1
  fi
fi

if [[ -n $reset ]]
then
  for sourcedir in $sortdir/*(N)
  do
    rm -rf $sourcedir
  done
fi

s.print() {
  local c1=00
  case ${1[1]} in
    +)  c1=32 ;;
    -)  c1=33 ;;
  esac
  printf "\e[1;30m%s\e[0m\n  \e[0;${c1}m%s\e[0m%s\n" "$(
    jq -r --arg name $2:t:r '.machine[] | select(.name == $name) | .description' $infodir/$2:t:r.json
  )" "$1:" "$2:h:t/$2:t"
}

for rom in $base/*.zip
do
  if tag -Nlg $rom | grep -q "^Ignore$"
  then
    # when tagged with "Ignore", find in .sorted and remove.
    find $sortdir -type f -name $rom:t | while read -r ignored
    do
      s.print -ignore $ignored
      rm $ignored
    done
    continue
  fi
  # speedup by ignoring roms already hardlined to .sorted.
  if find $sortdir -inum `stat -f %i $rom` | grep -q .
  then
    continue
  fi

  source=`jq --arg name $rom:t:r '.machine[] | select(
    .name == $name and .isbios != "yes" and .isdevice != "yes"
  ) | .source' $infodir/$rom:t:r.json`

  if [[ -n $source ]]
  then
    link=$sortdir/source-$source:t:r/$rom:t
    mkdir -p $link:h
    ln -f $rom $link
    s.print +linked $link
  fi
done

# remove sorted roms when not hardlinked more than once.
# when deleting roms from $base dir, .sorted should follow.
find $sortdir -type f ! -links +1 | while read -r removed
do
  rm $removed
  s.print -remove $removed
done
