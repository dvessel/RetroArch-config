#!/usr/bin/env zsh
#
# Generates database/mame.dat specific to the roms directory.
# For use with the MAME.lpl playlist to get proper names.
#
# requires standalone mame
#

romsdir=~/Games/Emulation/Arcade
basedir=~/Library/Application\ Support/RetroArch
mamedat=$basedir/database/mame.dat
append_dir=$basedir/database/mamexml
append_names=( $append_dir/*.xml(N:t:r) )

mame -listxml $romsdir/*.zip(:t:r) > $mamedat

if [[ -n $append_names ]]
then
  echo "Appending ${(j/,/)append_names}..."
  {
    # print main xml file, but omit closing </mame> tag.
    sed '/<\/mame>/d' $mamedat

    for name in $append_names
    do
      # print `/mame/machine` and omits the following:
      # 1. xml declaration line
      # 2. doctype & specs
      # 3. opening <mame> line
      # 4. closing </mame> line
      sed '/<?xml/d;/<!/d;/]>/d;/<mame/d;/<\/mame>/d' $append_dir/$name.xml
    done

    # print the closing tag.
    echo "</mame>"
  } > $mamedat:h.tmp && mv -f $mamedat:h.tmp $mamedat
fi
